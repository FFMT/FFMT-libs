buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

ext.buildnumber = 0

version = "1.8.1"
group= "fr.minecraftforgefrance"
archivesBaseName = "FFMT-libs"

sourceCompatibility = targetCompatibility = "1.6" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.6"
}

if (System.getenv().BUILD_NUMBER) {
    project.buildnumber = System.getenv().BUILD_NUMBER
	version += "." + System.getenv().BUILD_NUMBER
}
else {
    logger.lifecycle "SETTING BUILDNUMBER TO 0"
}

minecraft {
    version = "1.11.2-13.20.0.2262"
    runDir = "run"
    mappings = "snapshot_20170403"

    replace "@VERSION@", project.version
    replaceIn "FFMTLibs.java"

    makeObfSourceJar = true 
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'universal'
    version = "${project.version}-mc${project.minecraft.version}"
}

sourceJar {
    version = "${project.version}-mc${project.minecraft.version}"
}

configurations {
    deployerJars
}

repositories {
    maven {
        name = "mff"
        url = "http://files.minecraftforgefrance.fr/maven/"
    }
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}


uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        if (project.hasProperty('url')) {
            version = "${project.version}-mc${project.minecraft.version}"
            logger.info("Publishing to files server")
        	repository(url: url) {
            	authentication(userName: user, password: password)
            }
        }
        else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }
    }
}
