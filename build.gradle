buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

ext.buildnumber = 0

version = "1.3.0"
group= "fr.minecraftforgefrance.ffmtlibs"
archivesBaseName = "FFMT-libs"

if (System.getenv().BUILD_NUMBER) {
    project.buildnumber = System.getenv().BUILD_NUMBER
	version += "." + System.getenv().BUILD_NUMBER
}
else {
    logger.lifecycle "SETTING BUILDNUMBER TO 0"
}

minecraft {
    version = "1.7.2-10.12.0.1024"
    assetDir = "run/assets"
	
    replace '@VERSION@', project.version
}

sourceSets {
    main {
        java {
            srcDir 'common'
        }
        resources {
            srcDir 'resources'
        }
    }
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    appendix = 'universal'
    version = "${project.version}-mc${project.minecraft.version}"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
    version = "${project.version}-mc${project.minecraft.version}"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    appendix = 'javadoc'
    version = "${project.version}-mc${project.minecraft.version}"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf-dev'
    version = "${project.version}-mc${project.minecraft.version}"
}

build.dependsOn sourceJar, javadocJar, deobfJar

artifacts {
    archives sourceJar
    archives deobfJar
    archives javadocJar
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}
	
uploadArchives {
    dependsOn 'reobf'
    repositories {
        add project.repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployJars

        if (project.hasProperty("filesmaven")) {
            logger.info('Publishing to files server')
            repository(url: project.filesmaven.url) {
                authentication(userName: project.filesmaven.username, privateKey: project.filesmaven.key)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'FFMT libs'
            url 'https://github.com/FFMT/FFMT-libs'

            scm {
                url 'https://github.com/FFMT/FFMT-libs'
                connection 'scm:git:git://github.com/FFMT/FFMT-libs.git'
                developerConnection 'scm:git:git@github.com:FFMT/FFMT-libs.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/FFMT/FFMT-libs/issues'
            }

            developers {
                developer {
                    id 'robin4002'
                    name 'robin4002'
                    roles { role 'developer' }
                }
                developer {
                    id 'kevin_68'
                    name 'kevin_68'
                    roles { role 'developer' }
                }
                developer {
                    id 'elias54'
                    name 'elias54'
                    roles { role 'developer' }
                }
                developer {
                    id 'gugu42'
                    name 'gugu42'
                    roles { role 'developer' }
                }
                developer {
                    id 'letherman255'
                    name 'letherman255'
                    roles { role 'developer' }
                }
                developer {
                    id 'jglrxavpok'
                    name 'jglrxavpok'
                    roles { role 'developer' }
                }
                developer {
                    id 'Superloup10'
                    name 'Superloup10'
                    roles { role 'developer' }
                }
            }
        }
    }	
}
